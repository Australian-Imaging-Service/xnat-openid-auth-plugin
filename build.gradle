buildscript {
    ext {
        vXnat = "1.8.0"
    }
}

plugins {
    id "java"
    id "io.franzbecker.gradle-lombok" version "4.0.0"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "org.nrg.xnat.build.xnat-data-builder" version "1.8.0"
    id 'fr.brouillard.oss.gradle.jgitver' version '0.9.1'
}

group "au.edu.qcif.xnat.openid"
description "XNAT OpenID Authentication Provider"

jgitver {
    useDistance true
    useGitCommitID true
    useDirty true
    /* To play nice with literally everything else. */
    autoIncrementPatch false
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://nrgxnat.jfrog.io/nrgxnat/libs-release" }
    maven { url "https://nrgxnat.jfrog.io/nrgxnat/libs-snapshot" }
}

configurations {
    implementation.extendsFrom(implementAndInclude)

    all {
        exclude group: "javax.sql", module: "jdbc-stdext"
        exclude group: "javax.transaction", module: "jta"
    }
}

dependencyManagement.imports {
    mavenBom "org.nrg:parent:${vXnat}"
}

dependencies {
    compileOnly "org.nrg.xnat:web"
    compileOnly "org.springframework:spring-web"
    compileOnly "org.springframework.security:spring-security-config"
    compileOnly "org.springframework.security:spring-security-web"
	compileOnly "org.springframework.security.oauth:spring-security-oauth2"
    compileOnly "org.springframework.security:spring-security-jwt"
    compileOnly "org.apache.commons:commons-lang3"
    compileOnly "org.slf4j:slf4j-api"

    testImplementation "junit:junit"
    testImplementation "org.springframework:spring-test"

    annotationProcessor "org.projectlombok:lombok"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

jar {
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task fatJar(type: Jar) {
    archiveBaseName.set project.name + "-all"
    from {
        configurations.implementAndInclude.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF", "META-INF/*.DSA", "META-INF/*.RSA", "META-INF/*.RSA"
    }
    with jar
}

/* FIXME: fatJar isn't inheriting the manifest from jar, so hack around it. */
[jar, fatJar].each { it ->
    it.doFirst {
        manifest {
            attributes 'Application-Name': project.description,
                       'Build-Date': new Date(),
                       'Implementation-Sha': project.ext.git_sha1_full,
                       'Implementation-Version': project.version
        }
    }
}
